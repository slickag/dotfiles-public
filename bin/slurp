#!/usr/bin/env zsh

emulate -L zsh -o no_unset -o pipe_fail

zmodload zsh/system || return
zmodload -F zsh/files b:zf_mkdir b:zf_rm || return

function usage() {
  print -r -- 'usage: slurp path...

Create a tar.gz archive from the specified files and directories
and write it to stdout. If stdout is a tty, write to clipboard.
Directories are archived recursively.'
}

() {
  local -i OPTIND
  local opt OPTARG
  while getopts ":h" opt; do
    case $opt in
      *h) usage; return 0;;
      \?) print -ru2 "slurp: invalid option: -$OPTARG"; return 1;;
      :)  print -ru2 "slurp: missing required argument: -OPTARG"; return 1;;
      +?) print -ru2 "slurp: unhandled option: +$opt"; return 1;;
      *)  print -ru2 "slurp: unhandled option: -$opt"; return 1;;
    esac
  done

  if (( OPTIND == $# + 1 )); then
    usage >&2
    return 1
  fi

  if [[ ! -t 1 ]]; then
    local out=(command cat)
  elif (( $+commands[xclip] && $#DISPLAY )); then
    local out=(command xclip -selection clipboard -in)
  else
    print -ru2 "slurp: refusing to write archive contents to terminal"
    return 1
  fi

  local tmpdir
  if (( $+commands[mktemp] )); then
    tmpdir=$(command mktemp -d -- ${TMPDIR:-/tmp}/slurp.XXXXXXXXXX) || return
  else
    tmpdir=${TMPDIR:-/tmp}/slurp.tmp.$sysparams[pid]
    zf_rm -rf -- $tmpdir || return
    zf_mkdir -- $tmpdir || return
  fi

  {
    local arg
    for arg in "$@"; do
      if [[ ! -e $arg ]]; then
        print -ru2 "slurp: file not found: $arg"
        return 1
      fi
      local target=${arg:A}
      if [[ -z $target(#qN.) && -z $target(#qN/) ]]; then
        print -ru2 "slurp: unsupported file type: $arg"
        return 1
      fi
      if [[ ${tmpdir:A} == $target(|/*) ]]; then
        print -ru2 "slurp: cannot slurp: $arg"
        return 1
      fi
      if [[ -e $tmpdir/${arg:a:t} ]]; then
        print -ru2 "slurp: duplicate file name: ${arg:a:t}"
        return 1
      fi
      command ln -s -- $target $tmpdir/${arg:a:t} || return
    done

    local tar_version tar_opt
    if tar_version=$(command tar --version 2>/dev/null) && [[ $tar_version == *'GNU tar'* ]]; then
      tar_opt=(--owner=0 --group=0)
    fi

    command tar -C $tmpdir $tar_opt -czhf - -- "${@:a:t}" | $out || return
  } always {
    zf_rm -rf -- $tmpdir
  }
} "$@"
